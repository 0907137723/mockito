assert project == rootProject

Properties props = new Properties()
File versionFile = rootProject.file("version.properties")
props.load(new FileInputStream(versionFile))

project.version = props.version

logger.lifecycle "Version: $project.version"

task incrementVersion {
    description "Increments version in 'version.properties' file."
    doLast {
        try {
            List numbers = project.version.split('\\.')
            //TODO remove -dev hack once we're happy with releases.
            //TODO version file will have -SNAPSHOT name but Travis with remove -SNAPSHOT during the build
            int micro = (numbers.pop() - "-dev") as Integer
            numbers << micro + 1
            def newVersion = numbers.join('.') + "-dev"
            def updatedContent = versionFile.text.replaceAll("(?s)version=(.*?)\n", "version=$newVersion\n")
            versionFile.text = updatedContent
            logger.lifecycle("Current content of '{}':\n----------\n{}\n----------", versionFile.name, updatedContent)
        } catch (Exception e) {
            throw new GradleException("Unable to increment minor version.", e)
        }
    }
}